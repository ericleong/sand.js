<!DOCTYPE HTML>
<html>
<head>
	<meta charset="UTF-8">

	<meta name="viewport" content="width=device-width, initial-scale=1">

	<title>sand.js</title>

	<script src="js/sand.js"></script>

	<script id="shader-fs-copy" type="x-shader/x-fragment">
		varying highp vec2 vTextureCoord;

		uniform sampler2D uSampler;

		void main(void) {
			gl_FragColor = texture2D(uSampler, vTextureCoord);
		}
	</script>

	<script id="shader-vs-copy" type="x-shader/x-vertex">
		attribute vec3 aVertexPosition;
		attribute vec2 aTextureCoord;

		varying highp vec2 vTextureCoord;

		void main(void) {
			gl_Position = vec4(aVertexPosition, 1.0);

			vTextureCoord = aTextureCoord;
		}
	</script>

	<script id="shader-fs-advance" type="x-shader/x-fragment">
		varying highp vec2 vTextureCoord;

		varying highp vec2 vTextureCoordAbove;
		varying highp vec2 vTextureCoordAboveLeft;
		varying highp vec2 vTextureCoordAboveLeftLeft;
		varying highp vec2 vTextureCoordAboveRight;
		varying highp vec2 vTextureCoordAboveRightRight;

		varying highp vec2 vTextureCoordLeft;
		varying highp vec2 vTextureCoordLeftLeft;
		varying highp vec2 vTextureCoordRight;
		varying highp vec2 vTextureCoordRightRight;
		
		varying highp vec2 vTextureCoordBelow;
		varying highp vec2 vTextureCoordBelowLeft;
		varying highp vec2 vTextureCoordBelowLeftLeft;
		varying highp vec2 vTextureCoordBelowRight;
		varying highp vec2 vTextureCoordBelowRightRight;

		uniform sampler2D uSampler;

		uniform int uBias;

		void main(void) {
			mediump vec4 above = texture2D(uSampler, vTextureCoordAbove);
			mediump vec4 aboveleft = texture2D(uSampler, vTextureCoordAboveLeft);
			mediump vec4 aboveleftleft = texture2D(uSampler, vTextureCoordAboveLeftLeft);
			mediump vec4 aboveright = texture2D(uSampler, vTextureCoordAboveRight);
			mediump vec4 aboverightright = texture2D(uSampler, vTextureCoordAboveRightRight);
			mediump vec4 current = texture2D(uSampler, vTextureCoord);
			mediump vec4 leftleft = texture2D(uSampler, vTextureCoordLeftLeft);
			mediump vec4 left = texture2D(uSampler, vTextureCoordLeft);
			mediump vec4 right = texture2D(uSampler, vTextureCoordRight);
			mediump vec4 rightright = texture2D(uSampler, vTextureCoordRightRight);
			mediump vec4 below = texture2D(uSampler, vTextureCoordBelow);
			mediump vec4 belowleft = texture2D(uSampler, vTextureCoordBelowLeft);
			mediump vec4 belowleftleft = texture2D(uSampler, vTextureCoordBelowLeftLeft);
			mediump vec4 belowright = texture2D(uSampler, vTextureCoordBelowRight);
			mediump vec4 belowrightright = texture2D(uSampler, vTextureCoordBelowRightRight);

			if (current.g > 0.25 && current.r > 0.25) { // occupied cell
				if (vTextureCoord.y - 1.0 / 720.0 <= 0.0) { // floor
					gl_FragColor = current;
				} else if (below.g < 0.75) {
					gl_FragColor = below;
				} else if (uBias == 0) {
					if (belowleft.g < 0.75 && left.g < 0.75) { // left bias
						gl_FragColor = belowleft;
					} else if (belowright.g < 0.75 && right.g < 0.75 && (rightright.g < 0.75 || belowrightright.g < 0.75)) {
						gl_FragColor = belowright;
					} else {
						gl_FragColor = current;
					}
				} else {
					if (belowright.g < 0.75 && right.g < 0.75) { // right bias
						gl_FragColor = belowright;
					} else if (belowleft.g < 0.75 && left.g < 0.75 && (leftleft.g < 0.75 || belowleftleft.g < 0.75)) {
						gl_FragColor = belowleft;
					} else {
						gl_FragColor = current;
					}
				}
			} else if (current.g < 0.75) { // empty cell
				if (vTextureCoord.y + 1.0 / 720.0 >= 1.0) { // ceiling
					gl_FragColor = current;
				} else if (above.g > 0.25 && above.r > 0.25) {
					gl_FragColor = above;
				} else if (uBias == 0) { // left bias
					if (aboveright.g > 0.25 && right.g > 0.25 && aboveright.r > 0.25) {
						gl_FragColor = aboveright;
					} else if (aboveleft.g > 0.25 && left.g > 0.25 && (leftleft.g > 0.25 || aboveleftleft.g > 0.25) && vTextureCoord.x >= 0.0 && aboveleft.r > 0.25) {
						gl_FragColor = aboveleft;
					} else {
						gl_FragColor = current;
					}
				} else { // right bias
					if (aboveleft.g > 0.25 && left.g > 0.25 && aboveleft.r > 0.25) {
						gl_FragColor = aboveleft;
					} else if (aboveright.g > 0.25 && right.g > 0.25 && (rightright.g > 0.25 || aboverightright.g > 0.25) && vTextureCoord.x <= 1.0 && aboveright.r > 0.25) {
						gl_FragColor = aboveright;
					} else {
						gl_FragColor = current;
					}
				}
			} else {
				// not sure how this can happen
				gl_FragColor = current;
			}
		}
	</script>

	<script id="shader-vs-advance" type="x-shader/x-vertex">
		attribute vec3 aVertexPosition;
		attribute vec2 aTextureCoord;

		varying highp vec2 vTextureCoord;
		
		varying highp vec2 vTextureCoordAbove;
		varying highp vec2 vTextureCoordAboveLeft;
		varying highp vec2 vTextureCoordAboveLeftLeft;
		varying highp vec2 vTextureCoordAboveRight;
		varying highp vec2 vTextureCoordAboveRightRight;

		varying highp vec2 vTextureCoordLeft;
		varying highp vec2 vTextureCoordLeftLeft;
		varying highp vec2 vTextureCoordRight;
		varying highp vec2 vTextureCoordRightRight;
		
		varying highp vec2 vTextureCoordBelow;
		varying highp vec2 vTextureCoordBelowLeft;
		varying highp vec2 vTextureCoordBelowLeftLeft;
		varying highp vec2 vTextureCoordBelowRight;
		varying highp vec2 vTextureCoordBelowRightRight;

		void main(void) {
			gl_Position = vec4(aVertexPosition, 1.0);

			vTextureCoord = aTextureCoord;
			vTextureCoordAbove = aTextureCoord + vec2(0.0, 1.0 / 720.0);
			vTextureCoordAboveLeft = aTextureCoord + vec2(-1.0 / 1280.0, 1.0 / 720.0);
			vTextureCoordAboveLeftLeft = aTextureCoord + vec2(-2.0 / 1280.0, 1.0 / 720.0);
			vTextureCoordAboveRight = aTextureCoord + vec2(1.0 / 1280.0, 1.0 / 720.0);
			vTextureCoordAboveRightRight = aTextureCoord + vec2(2.0 / 1280.0, 1.0 / 720.0);

			vTextureCoordLeft = aTextureCoord + vec2(-1.0 / 1280.0, 0.0);
			vTextureCoordLeftLeft = aTextureCoord + vec2(-2.0 / 1280.0, 0.0);
			vTextureCoordRight = aTextureCoord + vec2(1.0 / 1280.0, 0.0);
			vTextureCoordRightRight = aTextureCoord + vec2(2.0 / 1280.0, 0.0);
		
			vTextureCoordBelow = aTextureCoord + vec2(0.0, -1.0 / 720.0);
			vTextureCoordBelowLeft = aTextureCoord + vec2(-1.0 / 1280.0, -1.0 / 720.0);
			vTextureCoordBelowLeftLeft = aTextureCoord + vec2(-2.0 / 1280.0, -1.0 / 720.0);
			vTextureCoordBelowRight = aTextureCoord + vec2(1.0 / 1280.0, -1.0 / 720.0);
			vTextureCoordBelowRightRight = aTextureCoord + vec2(2.0 / 1280.0, -1.0 / 720.0);
		}
	</script>
</head>

<body onload="start()">
	

	<canvas id="glcanvas" width="1280" height="720">Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.</canvas>
</body>
</html>