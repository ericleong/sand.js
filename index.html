<!DOCTYPE HTML>
<html>
<head>
	<meta charset="UTF-8">

	<meta name="viewport" content="width=device-width, initial-scale=1">

	<title>sand.js</title>

	<script src="js/lutGenerator.js"></script>
	<script src="js/sand.js"></script>

	<script id="shader-fs-copy" type="x-shader/x-fragment">
		varying highp vec2 vTextureCoord;

		uniform sampler2D uSampler;

		void main(void) {
			gl_FragColor = texture2D(uSampler, vTextureCoord);
		}
	</script>

	<script id="shader-vs-copy" type="x-shader/x-vertex">
		attribute vec3 aVertexPosition;
		attribute vec2 aTextureCoord;

		varying highp vec2 vTextureCoord;

		void main(void) {
			gl_Position = vec4(aVertexPosition, 1.0);

			vTextureCoord = aTextureCoord;
		}
	</script>

	<script id="shader-fs-advance" type="x-shader/x-fragment">
		varying highp vec2 vTextureCoord;

		varying highp vec2 vTextureCoordAbove;
		varying highp vec2 vTextureCoordAboveLeft;
		varying highp vec2 vTextureCoordAboveLeftLeft;
		varying highp vec2 vTextureCoordAboveRight;
		varying highp vec2 vTextureCoordAboveRightRight;

		varying highp vec2 vTextureCoordLeft;
		varying highp vec2 vTextureCoordLeftLeft;
		varying highp vec2 vTextureCoordRight;
		varying highp vec2 vTextureCoordRightRight;
		
		varying highp vec2 vTextureCoordBelow;
		varying highp vec2 vTextureCoordBelowLeft;
		varying highp vec2 vTextureCoordBelowLeftLeft;
		varying highp vec2 vTextureCoordBelowRight;
		varying highp vec2 vTextureCoordBelowRightRight;

		uniform sampler2D uSampler;
		uniform sampler2D uCells;
		uniform sampler2D uRules;

		uniform int uBias;

		mediump vec4 cellIndex(in mediump vec4 color) {
			mediump float blueScaled = color.b * 63.0; // out of 64

			mediump vec2 tile;
			tile.y = floor(floor(blueScaled) / 8.0);
			tile.x = floor(blueScaled) - (tile.y * 8.0);

			mediump vec2 cellPos;
			cellPos.x = (tile.x * 0.125) + 0.5 / 512.0 + (color.r * (0.125 - 1.0 / 512.0));
			cellPos.y = (tile.y * 0.125) + 0.5 / 512.0 + (color.g * (0.125 - 1.0 / 512.0));

			return texture2D(uCells, cellPos);
		}

		mediump vec4 swap(in mediump vec4 current, in mediump vec4 other) {
			return texture2D(uRules, vec2(cellIndex(current).r, cellIndex(other).r));
		}

		void main(void) {
			mediump vec4 above = texture2D(uSampler, vTextureCoordAbove);
			mediump vec4 aboveleft = texture2D(uSampler, vTextureCoordAboveLeft);
			mediump vec4 aboveleftleft = texture2D(uSampler, vTextureCoordAboveLeftLeft);
			mediump vec4 aboveright = texture2D(uSampler, vTextureCoordAboveRight);
			mediump vec4 aboverightright = texture2D(uSampler, vTextureCoordAboveRightRight);
			mediump vec4 current = texture2D(uSampler, vTextureCoord);
			mediump vec4 leftleft = texture2D(uSampler, vTextureCoordLeftLeft);
			mediump vec4 left = texture2D(uSampler, vTextureCoordLeft);
			mediump vec4 right = texture2D(uSampler, vTextureCoordRight);
			mediump vec4 rightright = texture2D(uSampler, vTextureCoordRightRight);
			mediump vec4 below = texture2D(uSampler, vTextureCoordBelow);
			mediump vec4 belowleft = texture2D(uSampler, vTextureCoordBelowLeft);
			mediump vec4 belowleftleft = texture2D(uSampler, vTextureCoordBelowLeftLeft);
			mediump vec4 belowright = texture2D(uSampler, vTextureCoordBelowRight);
			mediump vec4 belowrightright = texture2D(uSampler, vTextureCoordBelowRightRight);

			if (vTextureCoordBelow.y >= 0.0 && below.a < current.a) { // fall to below
				gl_FragColor = swap(current, below);
			} else if (vTextureCoordBelow.y >= 0.0 && uBias == 0 && belowleft.a < current.a && left.a <= belowleft.a) { // left bias
				gl_FragColor = swap(current, belowleft);
			} else if (vTextureCoordBelow.y >= 0.0 && uBias == 0 && belowright.a < current.a && right.a < belowright.a && (rightright.a <= belowright.a || belowrightright.a <= rightright.a)) {
				gl_FragColor = swap(current, belowright);
			} else if (vTextureCoordBelow.y >= 0.0 && uBias != 0 && belowright.a < current.a && right.a <= belowright.a) { // right bias
				gl_FragColor = swap(current, belowright);
			} else if (vTextureCoordBelow.y >= 0.0 && uBias != 0 && belowleft.a < current.a && left.a < belowleft.a && (leftleft.a <= belowleft.a || belowleftleft.a <= leftleft.a)) {
				gl_FragColor = swap(current, belowleft);
			} else if (vTextureCoordAbove.y <= 1.0 && above.a > current.a) { // take from above
				gl_FragColor = swap(current, above);
			} else if (vTextureCoordAbove.y <= 1.0 && uBias == 0 && aboveright.a > current.a && right.a >= aboveright.a) { // left bias
				gl_FragColor = swap(current, aboveright);
			} else if (vTextureCoordAbove.y <= 1.0 && uBias == 0 && aboveleft.a > current.a && left.a > aboveleft.a && (leftleft.a >= aboveleft.a || aboveleftleft.a >= leftleft.a) && vTextureCoord.x >= 0.0) {
				gl_FragColor = swap(current, aboveleft);
			} else if (vTextureCoordAbove.y <= 1.0 && uBias != 0 && aboveleft.a > current.a && left.a >= aboveleft.a) { // right bias
				gl_FragColor = swap(current, aboveleft);
			} else if (vTextureCoordAbove.y <= 1.0 && uBias != 0 && aboveright.a > current.a && right.a > aboveright.a && (rightright.a >= aboveleft.a || aboverightright.a >= rightright.a) && vTextureCoord.x <= 1.0) {
				gl_FragColor = swap(current, aboveright);
			} else {
				gl_FragColor = current;
			}
		}
	</script>

	<script id="shader-vs-advance" type="x-shader/x-vertex">
		attribute vec3 aVertexPosition;
		attribute vec2 aTextureCoord;

		varying highp vec2 vTextureCoord;
		
		varying highp vec2 vTextureCoordAbove;
		varying highp vec2 vTextureCoordAboveLeft;
		varying highp vec2 vTextureCoordAboveLeftLeft;
		varying highp vec2 vTextureCoordAboveRight;
		varying highp vec2 vTextureCoordAboveRightRight;

		varying highp vec2 vTextureCoordLeft;
		varying highp vec2 vTextureCoordLeftLeft;
		varying highp vec2 vTextureCoordRight;
		varying highp vec2 vTextureCoordRightRight;
		
		varying highp vec2 vTextureCoordBelow;
		varying highp vec2 vTextureCoordBelowLeft;
		varying highp vec2 vTextureCoordBelowLeftLeft;
		varying highp vec2 vTextureCoordBelowRight;
		varying highp vec2 vTextureCoordBelowRightRight;

		void main(void) {
			gl_Position = vec4(aVertexPosition, 1.0);

			vTextureCoord = aTextureCoord;
			vTextureCoordAbove = aTextureCoord + vec2(0.0, 1.0 / 720.0);
			vTextureCoordAboveLeft = aTextureCoord + vec2(-1.0 / 1280.0, 1.0 / 720.0);
			vTextureCoordAboveLeftLeft = aTextureCoord + vec2(-2.0 / 1280.0, 1.0 / 720.0);
			vTextureCoordAboveRight = aTextureCoord + vec2(1.0 / 1280.0, 1.0 / 720.0);
			vTextureCoordAboveRightRight = aTextureCoord + vec2(2.0 / 1280.0, 1.0 / 720.0);

			vTextureCoordLeft = aTextureCoord + vec2(-1.0 / 1280.0, 0.0);
			vTextureCoordLeftLeft = aTextureCoord + vec2(-2.0 / 1280.0, 0.0);
			vTextureCoordRight = aTextureCoord + vec2(1.0 / 1280.0, 0.0);
			vTextureCoordRightRight = aTextureCoord + vec2(2.0 / 1280.0, 0.0);
		
			vTextureCoordBelow = aTextureCoord + vec2(0.0, -1.0 / 720.0);
			vTextureCoordBelowLeft = aTextureCoord + vec2(-1.0 / 1280.0, -1.0 / 720.0);
			vTextureCoordBelowLeftLeft = aTextureCoord + vec2(-2.0 / 1280.0, -1.0 / 720.0);
			vTextureCoordBelowRight = aTextureCoord + vec2(1.0 / 1280.0, -1.0 / 720.0);
			vTextureCoordBelowRightRight = aTextureCoord + vec2(2.0 / 1280.0, -1.0 / 720.0);
		}
	</script>
</head>

<body onload="start()">
	<canvas id="glcanvas" width="1280" height="720">Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.</canvas>
	<p id="config">cell empty 0 0 0 0.5
cell sand 255 255 255 1.0
cell wall 127 127 127 0.5
cell fire 255 0 0 0.1
rule empty sand sand empty
rule wall empty wall empty
rule wall sand wall sand
rule wall fire wall fire
rule empty fire empty empty
rule fire sand fire fire</p>
</body>>