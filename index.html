<!DOCTYPE HTML>
<html>
<head>
	<meta charset="UTF-8">

	<meta name="viewport" content="width=device-width, initial-scale=1">
	<meta name="theme-color" content="#212121">
	<meta name="apple-mobile-web-app-capable" content="yes">
	<meta name="apple-mobile-web-app-status-bar-style" content="black">

	<title>sand.js</title>

	<link rel="manifest" href="manifest.json">
	<link rel="icon" sizes="192x192" href="icon/launcher_icon.png">
	<link rel="stylesheet" type="text/css" href="css/font.css">
	<link rel="stylesheet" type="text/css" href="css/sandbox.css">

	<script src="js/config.js"></script>
	<script src="js/utils.js"></script>
	<script src="js/sand.js"></script>
	<script src="js/draw.js"></script>
	<script src="js/input.js"></script>
	<script src="js/sandbox.js"></script>

	<script id="shader-fs-draw" type="x-shader/x-fragment">
		varying highp vec2 vTextureCoord;

		uniform sampler2D uCells;
		uniform sampler2D uSampler;

		void main(void) {
			gl_FragColor = texture2D(uCells, vec2(texture2D(uSampler, vTextureCoord).r, 0.5));
		}
	</script>

	<script id="shader-fs-copy" type="x-shader/x-fragment">
		varying highp vec2 vTextureCoord;

		uniform sampler2D uSampler;

		void main(void) {
			gl_FragColor = texture2D(uSampler, vTextureCoord);
		}
	</script>

	<script id="shader-vs-draw" type="x-shader/x-vertex">
		attribute vec3 aVertexPosition;
		attribute vec2 aTextureCoord;

		varying highp vec2 vTextureCoord;

		void main(void) {
			gl_Position = vec4(aVertexPosition, 1.0);

			vTextureCoord = aTextureCoord;
		}
	</script>

	<script id="shader-fs-sand-left" type="x-shader/x-fragment">
		varying highp vec2 vTextureCoord;

		varying highp vec2 vTextureCoordAbove;
		varying highp vec2 vTextureCoordAboveAbove;
		varying highp vec2 vTextureCoordAboveLeft;
		varying highp vec2 vTextureCoordAboveRight;

		varying highp vec2 vTextureCoordLeft;
		varying highp vec2 vTextureCoordRight;
		
		varying highp vec2 vTextureCoordBelow;
		varying highp vec2 vTextureCoordBelowLeft;
		varying highp vec2 vTextureCoordBelowLeftLeft;
		varying highp vec2 vTextureCoordBelowRight;

		uniform sampler2D uSampler;
		uniform sampler2D uRules;

		mediump vec4 swap(in mediump vec4 current, in mediump vec4 other) {
			return texture2D(uRules, vec2(current.r, other.r));
		}

		void main(void) {
			mediump vec4 above = texture2D(uSampler, vTextureCoordAbove);
			mediump vec4 aboveabove = texture2D(uSampler, vTextureCoordAboveAbove);
			mediump vec4 aboveleft = texture2D(uSampler, vTextureCoordAboveLeft);
			mediump vec4 aboveright = texture2D(uSampler, vTextureCoordAboveRight);
			mediump vec4 current = texture2D(uSampler, vTextureCoord);
			mediump vec4 left = texture2D(uSampler, vTextureCoordLeft);
			mediump vec4 right = texture2D(uSampler, vTextureCoordRight);
			mediump vec4 below = texture2D(uSampler, vTextureCoordBelow);
			mediump vec4 belowleft = texture2D(uSampler, vTextureCoordBelowLeft);
			mediump vec4 belowleftleft = texture2D(uSampler, vTextureCoordBelowLeftLeft);
			mediump vec4 belowright = texture2D(uSampler, vTextureCoordBelowRight);

			if (vTextureCoordBelow.y >= 0.0 && below.a < current.a && above.a <= current.a) { // fall to below
				gl_FragColor = swap(current, below);
			} else if (vTextureCoordBelow.y >= 0.0 && belowleft.a < current.a && left.a <= belowleft.a) { // left bias
				gl_FragColor = swap(current, belowleft);
			} else if (vTextureCoordAbove.y <= 1.0 && above.a > current.a && aboveabove.a <= above.a) { // take from above
				gl_FragColor = swap(current, above);
			} else if (vTextureCoordAbove.y <= 1.0 && aboveright.a > current.a && right.a >= aboveright.a) { // left bias
				gl_FragColor = swap(current, aboveright);
			} else if (vTextureCoordBelow.y >= 0.0 && current.b < 0.5 && left.a < current.a && below.a >= current.a && belowleft.a >= current.a && aboveleft.a <= left.a && left.a <= belowleft.a && above.a < current.a && above.a <= left.a && belowleftleft.a >= left.a) { // move left
				mediump vec4 newCell = swap(current, left);
				newCell.g = 0.0;
				gl_FragColor = newCell;
			} else if (vTextureCoordAbove.y <= 1.0 && right.b < 0.5 && right.a > current.a && below.a >= current.a && below.a >= right.a && above.a <= current.a && aboveabove.a <= above.a && aboveright.a <= current.a && aboveright.a < right.a && right.a <= belowright.a && current.a <= belowleft.a) { // take from right
				mediump vec4 newCell = swap(current, right);
				newCell.g = 1.0;
				gl_FragColor = newCell;
			} else {
				current.g = 0.0;
				gl_FragColor = current;
			}
		}
	</script>

	<script id="shader-vs-sand-left" type="x-shader/x-vertex">
		attribute vec3 aVertexPosition;
		attribute vec2 aTextureCoord;

		varying highp vec2 vTextureCoord;
		
		varying highp vec2 vTextureCoordAbove;
		varying highp vec2 vTextureCoordAboveAbove;
		varying highp vec2 vTextureCoordAboveLeft;
		varying highp vec2 vTextureCoordAboveRight;

		varying highp vec2 vTextureCoordLeft;
		varying highp vec2 vTextureCoordRight;
		
		varying highp vec2 vTextureCoordBelow;
		varying highp vec2 vTextureCoordBelowLeft;
		varying highp vec2 vTextureCoordBelowLeftLeft;
		varying highp vec2 vTextureCoordBelowRight;

		uniform highp vec2 uSize;

		void main(void) {
			gl_Position = vec4(aVertexPosition, 1.0);

			vTextureCoord = aTextureCoord;
			vTextureCoordAbove = aTextureCoord + vec2(0.0, 1.0 / uSize.y);
			vTextureCoordAboveAbove = aTextureCoord + vec2(0.0, 2.0 / uSize.y);
			vTextureCoordAboveLeft = aTextureCoord + vec2(-1.0 / uSize.x, 1.0 / uSize.y);
			vTextureCoordAboveRight = aTextureCoord + vec2(1.0 / uSize.x, 1.0 / uSize.y);

			vTextureCoordLeft = aTextureCoord + vec2(-1.0 / uSize.x, 0.0);
			vTextureCoordRight = aTextureCoord + vec2(1.0 / uSize.x, 0.0);
		
			vTextureCoordBelow = aTextureCoord + vec2(0.0, -1.0 / uSize.y);
			vTextureCoordBelowLeft = aTextureCoord + vec2(-1.0 / uSize.x, -1.0 / uSize.y);
			vTextureCoordBelowLeftLeft = aTextureCoord + vec2(-2.0 / uSize.x, -1.0 / uSize.y);
			vTextureCoordBelowRight = aTextureCoord + vec2(1.0 / uSize.x, -1.0 / uSize.y);
		}
	</script>

	<script id="shader-fs-sand-right" type="x-shader/x-fragment">
		varying highp vec2 vTextureCoord;

		varying highp vec2 vTextureCoordAbove;
		varying highp vec2 vTextureCoordAboveAbove;
		varying highp vec2 vTextureCoordAboveLeft;
		varying highp vec2 vTextureCoordAboveRight;

		varying highp vec2 vTextureCoordLeft;
		varying highp vec2 vTextureCoordRight;
		
		varying highp vec2 vTextureCoordBelow;
		varying highp vec2 vTextureCoordBelowRight;
		varying highp vec2 vTextureCoordBelowRightRight;
		varying highp vec2 vTextureCoordBelowLeft;

		uniform sampler2D uSampler;
		uniform sampler2D uRules;

		mediump vec4 swap(in mediump vec4 current, in mediump vec4 other) {
			return texture2D(uRules, vec2(current.r, other.r));
		}

		void main(void) {
			mediump vec4 above = texture2D(uSampler, vTextureCoordAbove);
			mediump vec4 aboveabove = texture2D(uSampler, vTextureCoordAboveAbove);
			mediump vec4 aboveleft = texture2D(uSampler, vTextureCoordAboveLeft);
			mediump vec4 aboveright = texture2D(uSampler, vTextureCoordAboveRight);
			mediump vec4 current = texture2D(uSampler, vTextureCoord);
			mediump vec4 left = texture2D(uSampler, vTextureCoordLeft);
			mediump vec4 right = texture2D(uSampler, vTextureCoordRight);
			mediump vec4 below = texture2D(uSampler, vTextureCoordBelow);
			mediump vec4 belowright = texture2D(uSampler, vTextureCoordBelowRight);
			mediump vec4 belowrightright = texture2D(uSampler, vTextureCoordBelowRightRight);
			mediump vec4 belowleft = texture2D(uSampler, vTextureCoordBelowLeft);

			if (vTextureCoordBelow.y >= 0.0 && below.a < current.a && above.a <= current.a) { // fall to below
				gl_FragColor = swap(current, below);
			} else if (vTextureCoordBelow.y >= 0.0 && belowright.a < current.a && right.a <= belowright.a) { // right bias
				gl_FragColor = swap(current, belowright);
			} else if (vTextureCoordAbove.y <= 1.0 && above.a > current.a && aboveabove.a <= above.a) { // take from above
				gl_FragColor = swap(current, above);
			} else if (vTextureCoordAbove.y <= 1.0 && aboveleft.a > current.a && left.a >= aboveleft.a) { // right bias
				gl_FragColor = swap(current, aboveleft);
			} else if (vTextureCoordBelow.y >= 0.0 && current.g < 0.5 && right.a < current.a && below.a >= current.a && belowright.a >= current.a && aboveright.a <= right.a && right.a <= belowright.a && above.a < current.a && above.a <= right.a && belowrightright.a >= right.a) { // move right
				mediump vec4 newCell = swap(current, right);
				newCell.b = 0.0;
				gl_FragColor = newCell;
			} else if (vTextureCoordAbove.y <= 1.0 && left.g < 0.5 && left.a > current.a && below.a >= current.a && below.a >= left.a && above.a <= current.a && aboveabove.a <= above.a && aboveleft.a <= current.a && aboveleft.a < left.a && left.a <= belowleft.a && current.a <= belowright.a) { // take from left
				mediump vec4 newCell = swap(current, left);
				newCell.b = 1.0;
				gl_FragColor = newCell;
			} else {
				current.b = 0.0;
				gl_FragColor = current;
			}
		}
	</script>

	<script id="shader-vs-sand-right" type="x-shader/x-vertex">
		attribute vec3 aVertexPosition;
		attribute vec2 aTextureCoord;

		varying highp vec2 vTextureCoord;
		
		varying highp vec2 vTextureCoordAbove;
		varying highp vec2 vTextureCoordAboveAbove;
		varying highp vec2 vTextureCoordAboveLeft;
		varying highp vec2 vTextureCoordAboveRight;

		varying highp vec2 vTextureCoordLeft;
		varying highp vec2 vTextureCoordRight;
		
		varying highp vec2 vTextureCoordBelow;
		varying highp vec2 vTextureCoordBelowRight;
		varying highp vec2 vTextureCoordBelowRightRight;
		varying highp vec2 vTextureCoordBelowLeft;

		uniform highp vec2 uSize;

		void main(void) {
			gl_Position = vec4(aVertexPosition, 1.0);

			vTextureCoord = aTextureCoord;
			vTextureCoordAbove = aTextureCoord + vec2(0.0, 1.0 / uSize.y);
			vTextureCoordAboveAbove = aTextureCoord + vec2(0.0, 2.0 / uSize.y);
			vTextureCoordAboveLeft = aTextureCoord + vec2(-1.0 / uSize.x, 1.0 / uSize.y);
			vTextureCoordAboveRight = aTextureCoord + vec2(1.0 / uSize.x, 1.0 / uSize.y);

			vTextureCoordLeft = aTextureCoord + vec2(-1.0 / uSize.x, 0.0);
			vTextureCoordRight = aTextureCoord + vec2(1.0 / uSize.x, 0.0);
		
			vTextureCoordBelow = aTextureCoord + vec2(0.0, -1.0 / uSize.y);
			vTextureCoordBelowRight = aTextureCoord + vec2(1.0 / uSize.x, -1.0 / uSize.y);
			vTextureCoordBelowRightRight = aTextureCoord + vec2(2.0 / uSize.x, -1.0 / uSize.y);
			vTextureCoordBelowLeft = aTextureCoord + vec2(-1.0 / uSize.x, -1.0 / uSize.y);
		}
	</script>
</head>

<body onload="start()">
	<canvas id="sandbox">Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.</canvas>
	<div id="sidebar">
		<h1>sand.js</h1>
		<h2>cells</h2>
		<div id="cell-select"></div>
		<div id="settings"><button id="pause">pause</button><div><a id="save-sandbox">save</a><a id="load-sandbox">load</a></div><input type="file" id="file-input" accept="image/*" onchange="handleImages(this.files)"></div>
		<h2>config</h2>
		<textarea id="config" rows="20" cols="30" wrap="off">cell empty 0 0 0 0.5
cell sand 255 193 7 1.0
cell wall 158 158 158 1.0
cell water 0 145 234 0.9
cell plant 100 221 23 0.8
cell fire 213 0 0 0.1
rule empty sand sand empty
rule wall empty wall empty
rule wall sand wall sand
rule wall fire wall empty
rule empty fire empty empty
rule fire sand sand empty
rule plant empty plant empty
rule plant fire fire fire
rule plant sand plant sand
rule plant wall plant wall
rule empty water water empty
rule water plant plant plant
rule water sand sand water
rule water fire water water
rule water wall water wall</textarea>
		<button id="update">update</button>
	</div>
</body>
</html>